open list

(* An iterator of type [iterator a post] allows enumerating a series of
   elements of type [a] (presumably the elements of some underlying
   collection) and, once stopped or finished, recovering the permission [post]
   (which typically represents the ownership of the underlying collection). *)

(* ------------------------------------------------------------------------------ *)

(* An outcome is information returned by the iterator to the client. *)

data outcome =
  | Stopped
  | Done

(* ------------------------------------------------------------------------------ *)

(* Concretely, an iterator is either: *)
data mutable iterator (i : term) a (post : perm) =

    (* finished, in which case the permission [post] is available, and the field
       [outcome] indicates whether the iteration was stopped or finished by iti; *)
    IFinished { outcome: outcome | post }

    (* or not finished, in which case an element [x] is available for inspection;
       this element is accompanied with two continuations, [stop] and [next]. Both
       of these continuations require an abstract permission [pre], only one copy
       of which is available, so the user is able to call [stop], or [next], but
       not both. (This is equivalent to a single continuation that takes a Boolean
       argument. We use two continuations because this allows [stop] and [next]
       to have distinct return types.) Furthermore, [stop] and [next] require the
       ownership of [x], so the element can be inspected, but cannot be retained. *)
  | IAvailable { content: { pre : perm } (
      x: a,
      stop: (| consumes (x @ a * pre * i @ IAvailable { content: unknown })) -> (| i @ IFinished { outcome: outcome | post }),
      next: (| consumes (x @ a * pre * i @ IAvailable { content: unknown })) -> (| i @ iterator i a post)
      | pre
    )}

alias finished (i : term) (post : perm) : perm =
  i @ IFinished { outcome: outcome | post }

alias scratch (i : term) : perm =
  i @ IAvailable { content: unknown }

alias active (i : term) a (post : perm) : perm =
  i @ iterator i a post

alias stop_ty (i : term) (x : term) a (pre : perm) (post : perm) =
  (| consumes (x @ a * pre * scratch i)) -> (| finished i post)

alias next_ty (i : term) (x : term) a (pre : perm) (post : perm) =
  (| consumes (x @ a * pre * scratch i)) -> (| active i a post)

alias available (i : term) (x : term) a (post : perm) : perm =
  i @ IAvailable { content: { pre : perm } (
    =x,
    stop: stop_ty i x a pre post,
    next: next_ty i x a pre post
  | pre
  )}

(* ------------------------------------------------------------------------------ *)

(* An iterator API. *)

data offer (i : term) a (post : perm) =
    None {| finished i post }
  | Some { x: (x: a | available i x a post) }

val query [a, post : perm]
  (i: unknown | consumes active i a post)
: offer i a post =
  match i with
  | IFinished ->
      None
  | IAvailable ->
      let x, _, _ = i.content in
      Some { x }
  end

val stop [x : term, a, post : perm]
  (i: unknown | consumes (x @ a * available i x a post))
: (| finished i post) =
  let _, stop, _ = i.content in
  stop()

val next [x : term, a, post : perm]
  (i: unknown | consumes (x @ a * available i x a post))
: (| active i a post) =
  let _, _, next = i.content in
  next()

(* ------------------------------------------------------------------------------ *)

val new () : (i: unknown | scratch i) =
  IAvailable { content = () }

val finish [post : perm] (i: unknown, o: outcome | consumes (post * scratch i)) : (| finished i post) =
  tag of i <- IFinished;
  i.outcome <- o

(* The function [yield] captures the current continuations [stop] and [next]
   and uses them to construct an iterator. The iteration is suspended (i.e.
   no continuation is invoked) and the iterator is returned. *)

(* TEMPORARY syntax for currying would be nice here *)

val yield (i: unknown) :
  [a, pre : perm, post : perm] 
  (
    consumes x: a,
    stop: stop_ty i x a pre post,
    next: next_ty i x a pre post
    | consumes pre
    | consumes scratch i
  ) -> (| active i a post)
  =
  fun [a, pre : perm, post : perm] (
    consumes x: a,
    stop: stop_ty i x a pre post,
    next: next_ty i x a pre post
    | consumes pre
    | consumes scratch i
  ) : (| active i a post) =
    (* We capture the continuations and write them into [i]. *)
    i.content <- (x, stop, next)

(* ------------------------------------------------------------------------------ *)

(* One can use an iterator to iterate in the same style as with a normal
   [iter] function. *)

val rec use [a, s : perm, post : perm] (
  consumes i: unknown,
  f : (a | s) -> bool
  | consumes active i a post
  | s
) : (| finished i post) =
  match query i with
  | None ->
      (* The iterator is finished. *)
      ()
  | Some { x } ->
      (* The iterator is not finished. Submit the element [x] to the user
	 function [f], which returns a request to stop or continue. *)
      if f x then begin
	  (* If we should continue, invoke [next], which updates this
	     iterator; and continue. *)
	next i;
	use (i, f)
      end
      else
	(* If we should stop, invoke [stop]. *)
	stop i
  end

(* ------------------------------------------------------------------------------ *)

(* One can construct a list of the elements of an iterator. This requires
   the type [a] to be duplicable, because at the end, we own both the newly
   created list and the original data structure (represented by the abstract
   permission [post]). *)

val rec elements [a, post : perm] duplicable a => (
  consumes i: unknown
| consumes active i a post
) : (list a | post) =
  (* Create a reference to hold the list of elements. *)
  let r = newref nil in
  (* Iterate. *)
  use (
    i,
    fun (x: a | r @ ref (list a)) : bool =
      r := cons (x, !r);
      true
  );
  (* Done! *)
  !r

(* ------------------------------------------------------------------------------ *)

(* Once we have determined that the two streams are not equal, we
   must explicitly stop the iterator(s) that are not yet finished,
   in order to obtain the permissions [post1] and [post2]. *)

val rec equal [a, post1 : perm, post2 : perm, p : perm] (
  eq: (a, a | p) -> bool,
  consumes i1: unknown,
  consumes i2: unknown
| consumes active i1 a post1
| consumes active i2 a post2
| p
) : (bool | finished i1 post1 * finished i2 post2) =

  match query i1, query i2 with
  | None, None ->
      True
  | Some { x = x1 }, Some { x = x2 } ->
      if eq (x1, x2) then begin
	next i1;
	next i2;
	equal (eq, i1, i2)
      end
      else begin
	stop i1;
	stop i2;
	False
      end
  | Some, None ->
      stop i1;
      False
  | None, Some ->
      stop i2;
      False
  end

(*
Local Variables:
compile-command: "../mezzo miterator.mz"
End:
*)
